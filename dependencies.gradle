def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

ext {
    // Android build tools
    sdkVersion = 28
    minSdkVersion = 19
    buildToolsVersion = '28.0.1'

    // Gradle Plugins
    androidPluginVersion = '3.1.3'
    gradleVersionPluginVersion = '0.20.0'
    ossLicensesPluginVersion = '0.9.2'

    // Android Support / Design
    supportLibraryVersion = '28.0.0-beta01'
    constraintLayout = '2.0.0-alpha1'
    androidSupport = dependencyGroup {
        implementation "com.android.support:support-v4:$supportLibraryVersion"
        implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
        implementation "com.android.support:support-annotations:$supportLibraryVersion"
        implementation "com.android.support:design:$supportLibraryVersion"
        implementation "com.android.support:cardview-v7:$supportLibraryVersion"
        implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
        implementation "com.android.support.constraint:constraint-layout:$constraintLayout"
        kapt "com.android.databinding:compiler:$androidPluginVersion"
    }

    // Kotlin
    kotlinVersion = '1.2.60'
    mockitoKotlinVersion = '1.6.0'
    kotlin = dependencyGroup {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
        testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
    }

    // Reactive
    rxJavaVersion = '2.2.0'
    rxAndroidVersion = '2.0.2'
    traceurVersion = "1.0.1"
    rx = dependencyGroup {
        api "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
        api "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
        api "com.tspoon.traceur:traceur:$traceurVersion"
    }

    // OSS Licenses
    ossLicensesVersion = '12.0.1'
    oss = dependencyGroup {
        implementation "com.google.android.gms:play-services-oss-licenses-license:$ossLicensesVersion"
    }

    // About page
    aboutPageVersion = '1.2.4'
    aboutPage = dependencyGroup {
        implementation "com.github.medyo:android-about-page:$aboutPageVersion"
    }

    // Debug tools
    timberVersion = '4.7.1'
    stethoVersion = '1.5.0'
    leakcanaryVersion = '1.6.1'
    debugTools = dependencyGroup {
        implementation "com.jakewharton.timber:timber:$timberVersion"
        debugImplementation "com.facebook.stetho:stetho:$stethoVersion"
        debugImplementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
        debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
        releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
        testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    }

    // Testing
    junitVersion = '4.12'
    mockitoVersion = '2.21.0'
    elmyrVersion = '0.6.2'
    assertjVersion = '3.10.0'
    testing = dependencyGroup {
        testImplementation "junit:junit:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "com.github.xgouchet:Elmyr:$elmyrVersion"
        testImplementation "org.assertj:assertj-core:$assertjVersion"
    }

    // Debug Dependencies
    findbugsAnnotationsVersion = '3.0.1'
    javaxInjectVersion = '1'
}
