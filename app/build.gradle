apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.google.gms.oss.licenses.plugin'

apply from: "$project.rootDir/dependencies.gradle"

android {

    compileSdkVersion sdkVersion
    buildToolsVersion buildToolsVersion

    defaultConfig {
        applicationId "fr.xgouchet.packageexplorer"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 14
        versionName "2.2"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    /** Stores the signing configs information */
    signingConfigs {
        debug {
            storeFile file("${projectDir.parentFile.parentFile.absolutePath}/.publish/debug.keystore")
        }

        release {
            Properties props = new Properties()
            props.load(new FileInputStream("${projectDir.parentFile.parentFile.absolutePath}/.publish/keystores.properties"))
            storeFile file("${projectDir.parentFile.parentFile.absolutePath}/.publish/xgouchet.keystore")

            storePassword props.releaseStorePassword
            keyAlias props.releaseKeyAlias
            keyPassword props.releaseKeyPassword
        }
    }

    /** The build types */
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    testOptions { unitTests.returnDefaultValues = true }

    dataBinding { enabled = true }
}

dependencies {

    androidSupport()
    kotlin()
    rx()
    oss()
    aboutPage()

    debugTools()

    implementation 'com.github.xgouchet:AXML:v1.0.1'
}